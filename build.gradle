project.ext {
  groupId = 'de.hypoport.efi'
  artifactId = 'bausparschnittstelle'
  version = new Date().format('yyyy-MM-dd-HH-mm-ss')
  nexusUrl = 'http://nexus-beta.hypoport.local/nexus/content/repositories/efi-releases'
}

buildscript {
  ext {
    springBootStarterVersion = '1.2.5.RELEASE'
  }
  repositories {
    mavenLocal()
    maven { url "http://nexus-beta.hypoport.local/nexus/content/repositories/efi" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootStarterVersion")
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'spring-boot'

sourceSets {
  main {
    output.classesDir = "$buildDir/classes/main"
    output.resourcesDir = "$buildDir/classes/main"
  }
  test {
    output.classesDir = "$buildDir/classes/test"
    output.resourcesDir = "$buildDir/classes/test"
  }
}

repositories {
  mavenLocal()
  maven { url 'http://nexus-beta.hypoport.local/nexus/content/repositories/efi' }
}

compileJava {
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

configurations {
  all*.exclude module: 'spring-boot-starter-logging'
}

dependencies {

  def springVersion = '4.1.4.RELEASE'
  def slf4jVersion = '1.7.6'
  def log4jVersion = '2.0-rc1'
  def springfoxVersion = '2.1.1'

  compile("org.springframework.boot:spring-boot-starter:$springBootStarterVersion") {
    exclude module: "spring-boot-starter-logging"
  }
  compile("org.springframework.boot:spring-boot-starter-web:$springBootStarterVersion") {
    exclude module: "spring-boot-starter-logging"
    exclude module: "logback-classic"
  }
  compile("org.springframework.boot:spring-boot-starter-actuator:$springBootStarterVersion")
  compile("org.springframework.boot:spring-boot-starter-log4j2:$springBootStarterVersion")
  compile("org.springframework:spring-core:$springVersion")


  compile('commons-io:commons-io:2.4')
  compile("org.slf4j:slf4j-api:$slf4jVersion")
  compile("org.slf4j:jcl-over-slf4j:$slf4jVersion")
  compile("org.slf4j:jul-to-slf4j:$slf4jVersion")
  compile("org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion")
  compile("org.apache.logging.log4j:log4j-api:$log4jVersion")
  compile("org.apache.logging.log4j:log4j-core:$log4jVersion")

  compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.0')

  compile("io.springfox:springfox-swagger2:${springfoxVersion}")
  compile("io.springfox:springfox-swagger-ui:${springfoxVersion}")

  testCompile('org.easytesting:fest-assert:1.4')
  testCompile('org.mockito:mockito-all:1.9.5')
  testCompile('junit:junit:4.11')
}


task tagGit << {
  exec {
    commandLine 'git', 'tag', "${project.ext.version}"
  }
  exec {
    commandLine 'git', 'push', '--tags'
  }
}
task wrapper(type: Wrapper) {
  gradleVersion = '2.1'
  distributionUrl = 'http://nexus-beta.hypoport.local/nexus/service/local/repositories/hypoport-shared-releases/content/gradle/gradle/2.1/gradle-2.1.zip'
}